<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx" 
	autoDrawBackground="false" mouseDown="mouseDownHandler(event);"
	width="100%" height="90">
	<fx:Script>
		<![CDATA[
			import flash.utils.getQualifiedClassName;
			
			import mx.core.DragSource;
			import mx.core.UIComponent;
			import mx.managers.DragManager;
			
			import us.ut.lib.slcpl.model.vo.Sticker;
			
			/**
			 * Starts drag process on mouse-down.
			 */
			protected function mouseDownHandler(event:MouseEvent):void
			{
				if (data)
				{
					var dragSource:DragSource = new DragSource();
					dragSource.addData(data, getQualifiedClassName(Sticker));
					
					// Borrow the bitmap data from the renderer so we don't have to make another
					// copy.
					var sourceBitmapData:BitmapData = Bitmap(image.content).bitmapData;
					var bitmap:Bitmap = new Bitmap(sourceBitmapData);

					var proxy:UIComponent = new UIComponent();
					proxy.addChild(bitmap);
					
					// Center the drag proxy under the mouse.
					DragManager.doDrag(this, dragSource, event, proxy, 
						bitmap.width / 2 - this.mouseX, 
						bitmap.height / 2 - this.mouseY);
				}
			}
		]]>
	</fx:Script>
	
	<mx:Image id="image" source="{Sticker(data).previewUrl}" maxWidth="90" maxHeight="90"
			horizontalCenter="0" verticalCenter="0" smoothBitmapContent="true"/>
	
</s:ItemRenderer>
